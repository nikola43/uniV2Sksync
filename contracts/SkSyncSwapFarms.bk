// SPDX-License-Identifier: MIT

pragma solidity >=0.6.12;

import "./libraries/SafeMath.sol";
import "./interfaces/IBEP20.sol";
import "./SafeBEP20.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

import "./SkSyncSwapToken.sol";

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() internal {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and make it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// import "@nomiclabs/buidler/console.sol";

// MasterChef is the master of Cake. He can make Cake and he is a fair guy.
//
// Note that it"s ownable and the owner wields tremendous power. The ownership
// will be transferred to a governance smart contract once CAKE is sufficiently
// distributed and the community can show to govern itself.
//
// Have fun reading it. Hopefully it"s bug-free. God bless.
contract SkSyncSwapFarms is Ownable, ReentrancyGuard {
    using SafeMath for uint256;
    using SafeBEP20 for IBEP20;

    // Info of each user.
    struct UserInfo {
        uint256 amount; // How many LP tokens the user has provided.
        uint256 rewardDebt; // Reward debt. See explanation below.
        //
        // We do some fancy math here. Basically, any point in time, the amount of SkSyncSwapToken
        // entitled to a user but is pending to be distributed is:
        //
        //   pending reward = (user.amount * pool.accSkSyncSwapTokenPerShare) - user.rewardDebt
        //
        // Whenever a user deposits or withdraws LP tokens to a pool. Here's what happens:
        //   1. The pool's `accSkSyncSwapTokenPerShare` (and `lastRewardBlock`) gets updated.
        //   2. User receives the pending reward sent to his/her address.
        //   3. User's `amount` gets updated.
        //   4. User's `rewardDebt` gets updated.
    }

    // Info of each pool.
    struct PoolInfo {
        IBEP20 lpToken; // Address of LP token contract.
        uint256 allocPoint; // How many allocation points assigned to this pool. SkSyncSwapTokens to distribute per block.
        uint256 lastRewardTimestamp; // Last block number that SkSyncSwapTokens distribution occurs.
        uint256 accSkSyncSwapTokenPerShare; // Accumulated SkSyncSwapTokens per share, times 1e12. See below.
        uint256 lpSupply;
    }

    // The SkSyncSwapToken TOKEN!
    SkSyncSwapToken public skSyncSwapToken;
    // SkSyncSwapToken tokens created per block.
    uint256 public skSyncSwapTokenPerTimestamp;
    // Bonus muliplier for early skSyncSwapToken makers.
    uint256 public constant BONUS_MULTIPLIER = 1;

    // Info of each pool.
    PoolInfo[] public poolInfo;
    // Info of each user that stakes LP tokens.
    mapping(uint256 => mapping(address => UserInfo)) public userInfo;
    // Total allocation points. Must be the sum of all allocation points in all pools.
    uint256 public totalAllocPoint = 0;

    event Deposit(address indexed user, uint256 indexed pid, uint256 amount);
    event Withdraw(address indexed user, uint256 indexed pid, uint256 amount);
    event EmergencyWithdraw(
        address indexed user,
        uint256 indexed pid,
        uint256 amount
    );
    event UpdateEmissionRate(
        address indexed user,
        uint256 skSyncSwapTokenPerTimestamp
    );
    event addPool(uint256 indexed pid, address lpToken, uint256 allocPoint);
    event setPool(uint256 indexed pid, address lpToken, uint256 allocPoint);
    event UpdateStartBlock(uint256 newStartBlock);

    constructor(
        SkSyncSwapToken _skSyncSwapToken,
        uint256 _skSyncSwapTokenPerTimestamp
    ) {
        skSyncSwapToken = _skSyncSwapToken;
        skSyncSwapTokenPerTimestamp = _skSyncSwapTokenPerTimestamp;
    }

    function poolLength() external view returns (uint256) {
        return poolInfo.length;
    }

    mapping(IBEP20 => bool) public poolExistence;
    modifier nonDuplicated(IBEP20 _lpToken) {
        require(poolExistence[_lpToken] == false, "nonDuplicated: duplicated");
        _;
    }

    // Add a new lp to the pool. Can only be called by the owner.
    function add(
        uint256 _allocPoint,
        IBEP20 _lpToken,
        bool _withUpdate
    ) external onlyOwner nonDuplicated(_lpToken) {
        // valid ERC20 token
        _lpToken.balanceOf(address(this));

        if (_withUpdate) {
            massUpdatePools();
        }

        totalAllocPoint = totalAllocPoint.add(_allocPoint);
        poolExistence[_lpToken] = true;
        poolInfo.push(
            PoolInfo({
                lpToken: _lpToken,
                allocPoint: _allocPoint,
                lastRewardTimestamp: block.timestamp,
                accSkSyncSwapTokenPerShare: 0,
                lpSupply: 0
            })
        );

        emit addPool(poolInfo.length - 1, address(_lpToken), _allocPoint);
    }

    // Update the given pool's SkSyncSwapToken allocation point and deposit fee. Can only be called by the owner.
    function set(
        uint256 _pid,
        uint256 _allocPoint,
        bool _withUpdate
    ) external onlyOwner {
        if (_withUpdate) {
            massUpdatePools();
        }
        totalAllocPoint = totalAllocPoint.sub(poolInfo[_pid].allocPoint).add(
            _allocPoint
        );
        poolInfo[_pid].allocPoint = _allocPoint;

        emit setPool(_pid, address(poolInfo[_pid].lpToken), _allocPoint);
    }

    // Return reward multiplier over the given _from to _to block.
    function getMultiplier(
        uint256 _from,
        uint256 _to
    ) public pure returns (uint256) {
        return _to.sub(_from);
    }

    // View function to see pending SkSyncSwapTokens on frontend.
    function pendingSkSyncSwapToken(
        uint256 _pid,
        address _user
    ) external view returns (uint256) {
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][_user];
        uint256 accSkSyncSwapTokenPerShare = pool.accSkSyncSwapTokenPerShare;
        if (
            block.timestamp > pool.lastRewardTimestamp &&
            pool.lpSupply != 0 &&
            totalAllocPoint > 0
        ) {
            uint256 multiplier = getMultiplier(
                pool.lastRewardTimestamp,
                block.timestamp
            );
            uint256 skSyncSwapTokenReward = multiplier
                .mul(skSyncSwapTokenPerTimestamp)
                .mul(pool.allocPoint)
                .div(totalAllocPoint);
            accSkSyncSwapTokenPerShare = accSkSyncSwapTokenPerShare.add(
                skSyncSwapTokenReward.mul(1e12).div(pool.lpSupply)
            );
        }
        return
            user.amount.mul(accSkSyncSwapTokenPerShare).div(1e12).sub(
                user.rewardDebt
            );
    }

    // Update reward variables for all pools. Be careful of gas spending!
    function massUpdatePools() public {
        uint256 length = poolInfo.length;
        for (uint256 pid = 0; pid < length; ++pid) {
            updatePool(pid);
        }
    }

    // Update reward variables of the given pool to be up-to-date.
    function updatePool(uint256 _pid) public {
        PoolInfo storage pool = poolInfo[_pid];
        if (block.timestamp <= pool.lastRewardTimestamp) {
            return;
        }
        if (pool.lpSupply == 0 || pool.allocPoint == 0) {
            pool.lastRewardTimestamp = block.timestamp;
            return;
        }
        uint256 multiplier = getMultiplier(pool.lastRewardTimestamp, block.timestamp);
        uint256 skSyncSwapTokenReward = multiplier
            .mul(skSyncSwapTokenPerTimestamp)
            .mul(pool.allocPoint)
            .div(totalAllocPoint);
        skSyncSwapToken.mintFor(address(this), skSyncSwapTokenReward);
        pool.accSkSyncSwapTokenPerShare = pool.accSkSyncSwapTokenPerShare.add(
            skSyncSwapTokenReward.mul(1e12).div(pool.lpSupply)
        );
        pool.lastRewardTimestamp = block.timestamp;
    }

    // Deposit LP tokens to MasterChef for SkSyncSwapToken allocation.
    function deposit(uint256 _pid, uint256 _amount) external nonReentrant {
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][msg.sender];
        updatePool(_pid);
        if (user.amount > 0) {
            uint256 pending = user
                .amount
                .mul(pool.accSkSyncSwapTokenPerShare)
                .div(1e12)
                .sub(user.rewardDebt);
            if (pending > 0) {
                safeSkSyncSwapTokenTransfer(msg.sender, pending);
            }
        }
        if (_amount > 0) {
            uint256 balanceBefore = pool.lpToken.balanceOf(address(this));
            pool.lpToken.safeTransferFrom(
                address(msg.sender),
                address(this),
                _amount
            );
            _amount = pool.lpToken.balanceOf(address(this)) - balanceBefore;

            user.amount = user.amount.add(_amount);
            pool.lpSupply = pool.lpSupply.add(_amount);
        }
        user.rewardDebt = user.amount.mul(pool.accSkSyncSwapTokenPerShare).div(
            1e12
        );
        emit Deposit(msg.sender, _pid, _amount);
    }

    // Withdraw LP tokens from MasterChef.
    function withdraw(uint256 _pid, uint256 _amount) external nonReentrant {
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][msg.sender];
        require(user.amount >= _amount, "withdraw: not good");
        updatePool(_pid);
        uint256 pending = user
            .amount
            .mul(pool.accSkSyncSwapTokenPerShare)
            .div(1e12)
            .sub(user.rewardDebt);
        if (pending > 0) {
            safeSkSyncSwapTokenTransfer(msg.sender, pending);
        }
        if (_amount > 0) {
            user.amount = user.amount.sub(_amount);
            pool.lpToken.safeTransfer(address(msg.sender), _amount);
            pool.lpSupply = pool.lpSupply.sub(_amount);
        }
        user.rewardDebt = user.amount.mul(pool.accSkSyncSwapTokenPerShare).div(
            1e12
        );
        emit Withdraw(msg.sender, _pid, _amount);
    }

    // Withdraw without caring about rewards. EMERGENCY ONLY.
    function emergencyWithdraw(uint256 _pid) external nonReentrant {
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][msg.sender];
        uint256 amount = user.amount;
        user.amount = 0;
        user.rewardDebt = 0;
        pool.lpToken.safeTransfer(address(msg.sender), amount);

        if (pool.lpSupply >= amount) {
            pool.lpSupply = pool.lpSupply.sub(amount);
        } else {
            pool.lpSupply = 0;
        }

        emit EmergencyWithdraw(msg.sender, _pid, amount);
    }

    // Safe skSyncSwapToken transfer function, just in case if rounding error causes pool to not have enough SkSyncSwapTokens.
    function safeSkSyncSwapTokenTransfer(
        address _to,
        uint256 _amount
    ) internal {
        uint256 skSyncSwapTokenBal = skSyncSwapToken.balanceOf(address(this));
        bool transferSuccess = false;
        if (_amount > skSyncSwapTokenBal) {
            transferSuccess = skSyncSwapToken.transfer(_to, skSyncSwapTokenBal);
        } else {
            transferSuccess = skSyncSwapToken.transfer(_to, _amount);
        }
        require(
            transferSuccess,
            "safeSkSyncSwapTokenTransfer: transfer failed"
        );
    }

    //SkSyncSwapToken has to add hidden dummy pools inorder to alter the emission, here we make it simple and transparent to all.
    function updateEmissionRate(
        uint256 _skSyncSwapTokenPerTimestamp
    ) external onlyOwner {
        massUpdatePools();
        skSyncSwapTokenPerTimestamp = _skSyncSwapTokenPerTimestamp;
        emit UpdateEmissionRate(msg.sender, _skSyncSwapTokenPerTimestamp);
    }
}
